#include"ModelInfo.hlsli"
#include"Camera.hlsli"

cbuffer cbuff0 : register(b0)
{
    Camera cam;
}

//CPU側と合わせる
static const int MAX_NUM = 1024;
cbuffer cbuff1 : register(b1)
{
    matrix world[MAX_NUM];
}

cbuffer cbuff2 : register(b2)
{
    matrix bones[256]; //ボーン行列
}

Texture2D<float4> tex : register(t0);
SamplerState smp : register(s0);

cbuffer cbuff3 : register(b3)
{
    Material material;
}

struct VSOutput
{
    float4 svpos : SV_POSITION;
    float2 uv : TEXCOORD;
};

VSOutput VSmain(Vertex input, uint instanceID : SV_InstanceID)
{
    float4 resultPos = input.pos;
	
	//ボーン行列
	//BDEF4(ボーン4つ又は3つと、それぞれのウェイト値。ウェイト合計が1.0である保障はしない)
    if (input.boneNo[2] != NO_BONE)
    {
        int num;
        
        if (input.boneNo[3] != NO_BONE)    //ボーン４つ
        {
            num = 4;
        }
        else //ボーン３つ
        {
            num = 3;
        }
        
        matrix mat = bones[input.boneNo[0]] * input.weight[0];
        for (int i = 1; i < num; ++i)
        {
            mat += bones[input.boneNo[i]] * input.weight[i];
        }
        resultPos = mul(mat, input.pos);
    }
	//BDEF2(ボーン2つと、ボーン1のウェイト値(PMD方式))
    else if (input.boneNo[1] != NO_BONE)
    {
        matrix mat = bones[input.boneNo[0]] * input.weight[0];
        mat += bones[input.boneNo[1]] * (1 - input.weight[0]);
        
        resultPos = mul(mat, input.pos);
    }
	//BDEF1(ボーンのみ)
    else if (input.boneNo[0] != NO_BONE)
    {
        resultPos = mul(bones[input.boneNo[0]], input.pos);
    }
	
    VSOutput output;
    output.svpos = mul(cam.proj, mul(cam.view, mul(world[instanceID], resultPos)));
    output.uv = input.uv;
    return output;
}


float4 PSmain(VSOutput input) : SV_TARGET
{
    float4 col = tex.Sample(smp, input.uv);
    col.xyz += material.baseColor;
    return col;
}

float4 main( float4 pos : POSITION ) : SV_POSITION
{
	return pos;
}